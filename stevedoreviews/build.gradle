buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath lib.plugin.bintray
        classpath lib.plugin.dcendents
        classpath lib.plugin.dokka
    }
}

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'com.github.ben-manes.versions' version '0.21.0'
}



ext {
    bintrayRepo = 'Android'
    bintrayName = 'Stevedore-Views'

    publishedGroupId = 'com.artear.stevedore'
    libraryName = 'Stevedore-Views'
    artifact = 'stevedoreviews'

    libraryDescription = 'Stevedore Views library'

    siteUrl = 'https://github.com/Artear/app_lib_stevedoreviews_android'
    gitUrl = 'https://github.com/Artear/app_lib_stevedoreviews_android.git'

    developerId = 'artear'
    developerName = 'Artear Moviles'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]

}

static int getVersionCode(branch) {
    def versionCodeCommand = "git rev-list --count " + branch
    return versionCodeCommand.execute().text.trim().toInteger()
}

static def getBranchName() {
    return "git rev-parse --abbrev-ref HEAD".execute().text.trim()
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    def branch = getBranchName()
    def vCode = 0 + getVersionCode(branch)

    libraryVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${project.getName()}-${defaultConfig.versionName}-${variant.name}.aar"
        }
    }

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 28
        versionCode vCode
        versionName libraryVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //Test
    testImplementation lib.junit.library
    testImplementation lib.androidx.testCore
    testImplementation lib.mockito.library

    androidTestImplementation lib.androidx.testCore
    androidTestImplementation lib.androidx.extJunit
    androidTestImplementation lib.androidx.testRunner
    androidTestImplementation lib.espresso.core

    //Kotlin
    implementation lib.kotlin.standardLib

    //Artear
    implementation lib.artear.networking

    //Warning! include tools
    implementation lib.artear.domain

    //Stevedore
    implementation lib.artear.stevedoreItems
    implementation lib.artear.articleItem
    implementation lib.artear.headerItem
    implementation lib.artear.bannerItem
    implementation lib.artear.categoryItem
    implementation lib.artear.mediaItem

    //Support
    implementation lib.androidx.appcompat
    implementation lib.androidx.recyclerview
    implementation lib.androidx.constraintLayout

}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm',
                                'preview', 'b', 'ea'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-+]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

group = publishedGroupId
version = libraryVersion

apply from: 'publication.gradle'
apply from: 'bintray.gradle'